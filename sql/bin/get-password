#!/usr/bin/env perl

# To successfully create a new account, this script must be run from
# somewhere with the user's Kerberos tickets.

use strict;
use JSON;

my $user = $ARGV[0] || $ENV{USER} or die "Unable to determine the locker name";

sub getMyCnfInfo($) {
  my $path = shift;
  open(MYCNF, $path) or return;
  my $section;
  my %result = (host => "sql.mit.edu", user => $user);
  while (<MYCNF>) {
    $section = $1 if /\[([^]]+)\]/;
    if (/(\w+)\s*=\s*([^\r\n]*)/) {
      my ($key, $value) = ($1, $2);
      if ($section eq "mysql" or $section eq "client") {
	$result{$key} = $value;
      }
    }
  }
  return \%result;
}
my $cnfPath = "/mit/$user/.sql/my.cnf";
my $cnfinfo = getMyCnfInfo($cnfPath);

if ($cnfinfo and $cnfinfo->{'password'}) {
  print "$cnfinfo->{'host'}\t$cnfinfo->{'user'}\t$cnfinfo->{'password'}\n";
  exit;
}

# Athena 9 needs to use the locker.
$ENV{"PATH"} = $ENV{"PATH"} . ":/mit/remctl/bin";
open(REMCTL, "-|", qw|remctl sql.mit.edu account create|, $user);
my $out = do { local $/; <REMCTL> };
close(REMCTL);

$out = decode_json($out);

if ($out->{"status"} == 0) {
  mkdir "/mit/$user/.sql", 0700;
  system(qw|fs sa|, "/mit/$user/.sql", qw|system:anyuser none system:authuser none daemon.scripts write daemon.sql write|);
  symlink "/mit/$user/.sql/my.cnf", "/mit/$user/.my.cnf";

  open(MYCNF, ">", $cnfPath) or die "Can't write login information to $cnfPath: $!";
  print MYCNF "[client]\nhost=sql.mit.edu\nuser=$user\npassword=$out->{'password'}\n";
  close(MYCNF) or die "Can't write login information to $cnfPath: $!";
  print "sql.mit.edu\t$user\t$out->{'password'}\n";
} else {
  die $out->{"error"};
}
